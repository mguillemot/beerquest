<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx"
               xmlns:ui="com.beerquest.ui.*"
               creationComplete="init()"
               width="520" height="320">
    <fx:Style>
        @namespace mx "library://ns.adobe.com/flex/mx";

        @font-face
        {
            src: url("../../../assets/font/kronika.ttf")
        ;
            fontFamily: Kro
        ;
            embedAsCFF: false
        ;
            advancedAntiAliasing: true
        ;
        }

        mx|ToolTip {
            fontFamily: Kro;
            fontSize: 14;
            color: black;
            backgroundColor: #ff0000;
        }
    </fx:Style>
    <fx:Script><![CDATA[
        import com.adobe.serialization.json.JSON;
        import com.beerquest.Capacity;
        import com.beerquest.Constants;
        import com.beerquest.InstantEventBuffer;
        import com.beerquest.PlayerData;
        import com.beerquest.TokenType;
        import com.beerquest.events.GameEvent;
        import com.beerquest.ui.events.UiCapacityExecutionEvent;
        import com.beerquest.ui.events.UiGameEvent;
        import com.beerquest.ui.events.UiGameStartEvent;
        import com.beerquest.ui.events.UiScoreEvent;
        import com.beerquest.ui.events.UiTokenEvent;
        import com.greensock.OverwriteManager;
        import com.greensock.TweenLite;
        import com.greensock.plugins.ColorTransformPlugin;
        import com.greensock.plugins.TintPlugin;
        import com.greensock.plugins.TweenPlugin;

        import mx.controls.Alert;
        import mx.core.FlexGlobals;

        private function init():void {
            // Init TweenLite
            OverwriteManager.init();
            TweenPlugin.activate([ColorTransformPlugin, TintPlugin]);

            // Parameters passed to game
            _allowStart = true;
            var requiredVersion:Number = FlexGlobals.topLevelApplication.parameters.requiredVersion;
            if (requiredVersion > 0) {
                if (requiredVersion > Constants.VERSION) {
                    _allowStart = false;
                    Alert.show("Current: " + Constants.VERSION + "\nRequired: " + requiredVersion + "\n\nPlease empty your cache and reload this page.", "Wrong version");
                }
            } else {
                trace("WARN: No 'requiredVersion' found");
            }

            mode = FlexGlobals.topLevelApplication.parameters.mode;
            if (mode != "vs" && mode != "solo") {
                trace("WARN: No proper 'mode' found, defaulting to 'solo'");
                mode = "solo";
                _allowStart = false;
            }
            trace("Starting in mode: " + mode);

            if (mode == "vs") {
                goal = FlexGlobals.topLevelApplication.parameters.goal;
                if (goal <= 0) {
                    trace("WARN: No proper 'goal' found, defaulting to 100");
                    goal = 100;
                    _allowStart = false;
                }
                trace("Initial goal set to: " + goal);
            }

            token = FlexGlobals.topLevelApplication.parameters.token;
            if (token == null) {
                trace("WARN: No 'token' found");
                token = "none";
                _allowStart = false;
            }
            trace("Using the following API token: " + token);
        }

        private function startGame(me:PlayerData, seed:int):void {
            addEventListener(UiGameEvent.UI_GAME_EVENT, onUiGameEvent);
            Constants.GAME.start(goal, me, seed);
        }

        private function onUiGameEvent(e:UiGameEvent):void {
            if (e.event.type == GameEvent.GAME_OVER) {
                gameOver();
            }
        }

        private function onStartInfoReceived(e:Event):void {
            trace("Game infos received.");
            var data:Object = JSON.decode(_tokenLoader.data);
            trace("Seed is " + data.seed);
            _tokenLoader.removeEventListener(Event.COMPLETE, onStartInfoReceived);
            _tokenLoader = null;
            startGame(new PlayerData(Constants.GAME), data.seed);
        }

        private function onGameStart(e:UiGameStartEvent):void {
            goal += e.scoreRaise;
            trace("Goal raised to: " + goal);
            if (_allowStart) {
                // Retrieve game info from server using token
                var request:URLRequest = new URLRequest("/start");
                var data:URLVariables = new URLVariables();
                data.token = token;
                data.raise = e.scoreRaise;
                request.method = URLRequestMethod.POST;
                request.data = data;
                _tokenLoader = new URLLoader();
                _tokenLoader.addEventListener(Event.COMPLETE, onStartInfoReceived);
                try {
                    _tokenLoader.load(request);
                    trace("Load game infos requested.");
                } catch (e:ArgumentError) {
                    trace("Load game infos: an ArgumentError has occurred.");
                } catch (e:SecurityError) {
                    trace("Load game infos: a SecurityError has occurred.");
                }
            } else if (Constants.DEBUG) {
                startGame(new PlayerData(Constants.GAME), 1234); // TODO random()
            }
        }

        private function gameOver():void {
            var data:URLVariables = stats.stats.getForSerialization();
            data.token = token;
            data.game_version = Constants.VERSION.toString();
            data.score = Constants.GAME.me.score;
            data.flash_version = Capabilities.version;

            var request:URLRequest = new URLRequest("/end");
            request.method = URLRequestMethod.POST;
            request.data = data;
            _tokenLoader = new URLLoader();
            _tokenLoader.addEventListener(Event.COMPLETE, onEndInfoReceived);
            try {
                _tokenLoader.load(request);
                trace("Start post end...");
            } catch (e:ArgumentError) {
                trace("Post end: an ArgumentError has occurred.");
            } catch (e:SecurityError) {
                trace("Post end: a SecurityError has occurred.");
                onEndInfoReceived(null);
            }
        }

        private function onEndInfoReceived(e:Event):void {
            var data:Object;
            if (e != null) {
                data = JSON.decode(_tokenLoader.data);
            } else {
                data = {valid:true, personalHigh:false, barHigh:false};
//                data = {valid:true, personalHigh:true, barHigh:true}; // TODO debug only
            }
            _tokenLoader.removeEventListener(Event.COMPLETE, onEndInfoReceived);
            _tokenLoader = null;
            trace("Replay received by the server.");
            if (data.valid) {
                gameover.setResult(mode, Constants.GAME.me.score, Constants.GAME.scoreGoal, data.personalHigh, data.barHigh);
                TweenLite.to(gameover, 2, {y:0});
            } else {
                TweenLite.to(gameover, 2, {y:0});
                Alert.show("Server error while uploading score.");
            }
        }

        private function onScoreGained(e:UiScoreEvent):void {
            var x:Number, y:Number;
            if (e.capacity != null) {
                var capaLoc:Point = myStatus.locateCapacity(e.capacity);
                x = capaLoc.x;
                y = capaLoc.y;
            } else {
                x = e.stageX;
                y = e.stageY;
            }
            vfxLayer.addScoreEffect(e.beers, e.turns, x, y);
        }

        private function onTokenGained(e:UiTokenEvent):void {
            var entryPoint:Point = myStatus.tokenEntryPoint;
            var token:TokenType = e.token;
            vfxLayer.addTokenEffect(token, e.stageX, e.stageY, entryPoint.x, entryPoint.y);
            var timer:Timer = new Timer(EffectLayer.TOKEN_EFFECT_TIME_MS, 1);
            timer.addEventListener(TimerEvent.TIMER, function(e:TimerEvent):void {
                myStatus.addCollectedToken(token);
            });
            timer.start();
        }

        private function onAskForExecution(e:UiCapacityExecutionEvent):void {
            if (e.capacity == Capacity.BIG_BANG) {
                boardView.processEvent(e);
            } else {
                Constants.GAME.executeCapacity(e.capacity, null, InstantEventBuffer.INSTANCE);
            }
        }

        [Bindable]
        public var token:String;

        [Bindable]
        public var mode:String;

        [Bindable]
        public var goal:int = 0;

        private var _tokenLoader:URLLoader;
        private var _allowStart:Boolean;
        ]]></fx:Script>

    <!-- board -->
    <mx:Canvas x="200" y="0" width="320" height="320">
        <ui:BoardView id="boardView" UiScoreGained="onScoreGained(event)" UiTokenGained="onTokenGained(event)"/>
    </mx:Canvas>

    <!-- status bar -->
    <ui:PlayerStatusView id="myStatus" x="5" y="5" AskForExecution="onAskForExecution(event)"/>
    <ui:SoundButton id="soundButton" x="140" y="292" on="true"/>
    <ui:QualityButton x="170" y="292" on="true"/>

    <!-- game overlay -->
    <ui:GameStartOverlay id="gamestart" mode="{mode}" scoreGoal="{goal}" UiGameStart="onGameStart(event)"/>
    <ui:GameOverOverlay id="gameover" y="{-height}"/>

    <!-- modules -->
    <ui:SoundModule soundEnabled="{soundButton.on}"/>
    <ui:StatsModule id="stats" token="{token}"/>

    <!-- vfx layer -->
    <ui:EffectLayer id="vfxLayer" width="100%" height="100%"/>

    <!-- debug -->
    <mx:Label text="Available moves: {boardView.availableMoves}" x="0" y="0" visible="{Constants.DEBUG}"/>
    <mx:Label text="Current action: {boardView.currentAction}" x="0" y="32" visible="{Constants.DEBUG}"/>
    <mx:Label x="0" y="300" text="Version {Constants.VERSION} - DEBUG" color="0xff0000" fontWeight="bold" fontSize="14"
              visible="{Constants.DEBUG}"/>
</s:Application>
